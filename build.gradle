apply plugin: 'eclipse'
apply plugin: 'groovy'

VERSION="0.1"
STAGE="build/stage/gatks-$VERSION"
GATK_HOME=System.getenv("GATK_HOME")

// If GATK not defined explicitly, look for it in the local directory
if(!GATK_HOME) {
    GATK_HOME=new File(".").listFiles().grep { 
       it.isDirectory() && it.name.startsWith("GenomeAnalysisTK-") 
    }.sort {
        it.lastModified()
    }.collect { it.name }[0]
}

if(!GATK_HOME || !new File(GATK_HOME).exists()) {
  println("\nCould not find location of GATK. Please download it and unzip in this directory.\n")
  println("\nAlternatively, you can set the variable GATK_HOME to point to your GATK installation.\n")
  System.exit(1)
}

repositories {
    flatDir(dirs: file('local-lib'))
    mavenCentral()
}

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.2'
    compile files(fileTree(dir:'local-lib', includes:['*.jar']), "$GATK_HOME/GenomeAnalysisTK.jar")
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

task stage(dependsOn: jar) << {
  ant.mkdir(dir: STAGE)
  ant.mkdir(dir: "$STAGE/bin")
  ant.mkdir(dir: "$STAGE/lib")
  ant.copy(todir: "$STAGE/bin") {
    ant.fileset(dir: 'bin', includes: "**")
  }
  new File(STAGE+'/bin/gatks').text = 
          new File("bin/gatks").text.replaceAll("VERSION=0.0.0","VERSION=$VERSION").replaceAll("BUILDDATE=''","BUILDDATE="+String.valueOf(System.currentTimeMillis()))

  ant.copy(todir: "$STAGE/lib") {
    ant.fileset(dir: 'local-lib', includes: "*.jar")
    ant.fileset(dir: 'build/libs', includes: "gatks.jar")
  }
}

task dist(dependsOn: stage) << {
  ant.tar(destfile: "build/gatks-${VERSION}.tar") {
    ant.fileset(dir: "build/stage", includes: "gatks-${VERSION}/lib/**", excludes:"**/*.swp")
    ant.tarfileset(dir: "build/stage", filemode:"755", includes: "gatks-${VERSION}/bin/**", excludes:"**/*.swp")
  }
  ant.gzip(destfile:"build/gatks-${VERSION}.tar.gz", src: "build/gatks-${VERSION}.tar")
}
